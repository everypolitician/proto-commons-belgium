SELECT DISTINCT
  ?house ?houseLabel
  ?legislature ?legislatureLabel
  ?term ?termLabel
  ?termStart ?termEnd
  ?termSpecificPosition
WHERE {
  VALUES (?house ?position) {
    (wd:Q1347545 wd:Q15705021)
    (wd:Q1355042 wd:Q17619252)
    (wd:Q2200734 rdf:nil)
    (wd:Q2449493 wd:Q19351455)
    (wd:Q2477569 rdf:nil)
    (wd:Q435623 rdf:nil)
    (wd:Q1292 wd:Q15105064)
  }
  ?house (p:P361/ps:P361)* ?legislature .
      ?baseTerm p:P31|p:P279 [ ps:P279|ps:P31 wd:Q15238777 ; pq:P642 ?legislature ] .
      OPTIONAL { ?subTerm wdt:P31 ?baseTerm }

  BIND(COALESCE(?subTerm, ?baseTerm) AS ?term)

  OPTIONAL { ?term (wdt:P580|wdt:P571) ?termStart. }
  OPTIONAL { ?term (wdt:P582|wdt:P576) ?termEnd. }
  OPTIONAL { ?term (wdt:P155|wdt:P1365) ?termReplaces }
  OPTIONAL { ?term (wdt:P156|wdt:P1366) ?termReplacedBy }
  OPTIONAL {
    ?termSpecificPosition wdt:P31/wdt:P279* wd:Q4164871 ;
                          p:P279 [ ps:P279 ?position ;
                                   pq:P2937 ?term ] .
  }

  FILTER (!BOUND(?termEnd) || ?termEnd > NOW())
  FILTER (!BOUND(?termReplacedBy))
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,de,fr,nl". }
} ORDER BY ?termStart ?term
